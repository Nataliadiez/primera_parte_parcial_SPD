/*
-----------CONSIGNA PRIMERA PARTE DEL PARCIAL----------------------------------
	Diseñar un contador de 0 a 99 utilizando dos displays de 7 segmentos y tres botones para controlar la cuenta.
    Debes implementar la técnica de multiplexación para mostrar los dígitos en los displays.
    El contador debe comenzar en 0 y debes ser capaz de aumentar o disminuir su valor en una unidad con los botones.
    
---------------------------------------------------------------------
*/

#define B_PIN 13
#define A_PIN 12
#define F_PIN 11
#define G_PIN 10
#define E_PIN 7
#define D_PIN 6
#define C_PIN 5
#define BOTON_INCREMENTAR 4
#define BOTON_REDUCIR 3
#define BOTON_REINICIAR 2
#define DISPLAY_UNIDAD A4
#define DISPLAY_DECENA A5

int contador = 0;
int contadorDigito = 0;
int decena;
int unidad;
unsigned long tiempoAnterior = 0;
int intervaloDeTiempo = 200;

void encenderSegmento(int sf, int sa, int sb, int sg, int se, int sd, int sc);
void encenderDisplay(int estadoDecena, int estadoUnidades);
void mostrarNumeroEnDisplay(int numero);

/**
 * Funcion que setea los pines de los display y los botones
 */
void setup()
{
  pinMode(A_PIN, OUTPUT);
  pinMode(B_PIN, OUTPUT);
  pinMode(C_PIN, OUTPUT);
  pinMode(D_PIN, OUTPUT);
  pinMode(E_PIN, OUTPUT);
  pinMode(F_PIN, OUTPUT);
  pinMode(G_PIN, OUTPUT);
  pinMode(BOTON_INCREMENTAR, INPUT_PULLUP);
  pinMode(BOTON_REDUCIR, INPUT_PULLUP);
  pinMode(BOTON_REINICIAR, INPUT_PULLUP);
  pinMode(DISPLAY_UNIDAD, OUTPUT);
  pinMode(DISPLAY_DECENA, OUTPUT);
  Serial.begin(9600);
}

/**
 * comienzo del contador numerico 
 */
void loop()
{
  unsigned long tiempoActual = millis();
  if (tiempoActual - tiempoAnterior >= intervaloDeTiempo) {
    tiempoAnterior = tiempoActual;
  
    if (digitalRead(BOTON_INCREMENTAR) == LOW)
    {
      contador++;
      if(contador > 99)
      {
        contador = 0;
      }
    }

    if(digitalRead(BOTON_REDUCIR) == LOW)
    {
      contador--;
      if(contador < 0)
      {
        contador = 99;
      }
    }
    if(digitalRead(BOTON_REINICIAR) == LOW)
    {
      contador = 0;
      decena = 0;
      unidad = 0;
    }
  }
  
  mostrarNumeroEnDisplay(contador);
}

/**
 * Funcion que enciende los segmentos según el valor que reciben los parámetros (0, 1)
 * @param sa posicion segmento A.
 * @param sb posicion segmento B.
 * @param sc posicion segmento C.
 * @param sd posicion segmento D.
 * @param se posicion segmento E.
 * @param sf posicion segmento F.
 * @param sg posicion segmento G.
 */
void encenderSegmento(int sf, int sa, int sb, int sg, int se, int sd, int sc)
{
  digitalWrite(F_PIN, sf);
  digitalWrite(A_PIN, sa);
  digitalWrite(B_PIN, sb);
  digitalWrite(G_PIN, sg);
  digitalWrite(E_PIN, se);
  digitalWrite(D_PIN, sd);
  digitalWrite(C_PIN, sc);
}

/**
 * Función que establece el orden de display a utilizar
 * @param numero entero que representa el numero a mostrar
 */
void seleccionarNumero(int numero)
{
  switch(numero)
  {
    case 0:
        encenderSegmento(1,1,1,0,1,1,1); 
        break;
    case 1: 
        encenderSegmento(0,0,1,0,0,0,1); 
        break;
    case 2: 
        encenderSegmento(0,1,1,1,1,1,0); 
        break;
    case 3: 
        encenderSegmento(0,1,1,1,0,1,1); 
        break;
    case 4: 
        encenderSegmento(1,0,1,1,0,0,1); 
        break;
    case 5: 
        encenderSegmento(1,1,0,1,0,1,1); 
        break;
    case 6: 
        encenderSegmento(1,1,0,1,1,1,1); 
        break;
    case 7: 
      	encenderSegmento(0,1,1,0,0,0,1); 
        break;
    case 8:
        encenderSegmento(1,1,1,1,1,1,1); 
        break;
    case 9: 
        encenderSegmento(1,1,1,1,0,1,1); 
      break;
  }
}


/**
 * Función que establece el encendido de los display
 * @param estadoDecena estado del display de decenas
 * @param estadoUnidades estado del display de unidades
 */
void encenderDisplay(int estadoDecena, int estadoUnidades)
{
  digitalWrite(DISPLAY_DECENA, estadoDecena);
  digitalWrite(DISPLAY_UNIDAD, estadoUnidades);
}

/*
* Función que se encarga de mostrar un número en los display de 7 segmentos.
* @param numero número entero que se desea mostrar en los display.
*/
void mostrarNumeroEnDisplay(int numero){
    int decena = numero / 10;
    int unidad = numero % 10;
    seleccionarNumero(decena);
    encenderDisplay(0,1);
    delay(10);
    encenderDisplay(1,1);
    seleccionarNumero(unidad);
    encenderDisplay(1,0);
    delay(10);
    encenderDisplay(1,1);
}


